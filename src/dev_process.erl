%%% @doc This module contains the device implementation of AO processes
%%% in Converge. The core functionality of the module is in 'routing' requests
%%% for different functionality (scheduling, computing, and pushing messages)
%%% to the appropriate device. This is achieved by swapping out the device 
%%% of the process message with the necessary component in order to run the 
%%% execution, then swapping it back before returning. Computation is supported
%%% as a stack of devices, customizable by the user, while the scheduling
%%% device is (by default) a single device.
%%% 
%%% This allows the devices to share state as needed. Additionally, after each
%%% computation step the device caches the result at a path relative to the
%%% process definition itself, such that the process message's ID can act as an
%%% immutable reference to the process's growing list of interactions. See 
%%% `dev_process_cache' for details.
%%% 
%%% The external API of the device is as follows:
%%% ```
%%% GET /ID/Schedule:                Returns the messages in the schedule
%%% POST /ID/Schedule:               Adds a message to the schedule
%%% 
%%% GET /ID/Compute/[IDorSlotNum]:   Returns the state of the process after 
%%%                                  applying a message
%%% GET /ID/Now:                     Returns the `/Results' key of the latest 
%%%                                  computed message
%%% '''
%%% 
%%% An example process definition will look like this:
%%% ```
%%%     Device: Process/1.0
%%%     Scheduler-Device: Scheduler/1.0
%%%     Execution-Device: Stack/1.0
%%%     Execution-Stack: "Scheduler/1.0", "Cron/1.0", "WASM/1.0", "PoDA/1.0"
%%%     Cron-Frequency: 10-Minutes
%%%     WASM-Image: WASMImageID
%%%     PoDA:
%%%         Device: PoDA/1.0
%%%         Authority: A
%%%         Authority: B
%%%         Authority: C
%%%         Quorum: 2
%%% '''
%%%
%%% Runtime options:
%%%     Cache-Frequency: The number of assignments that will be computed 
%%%                      before the full (restorable) state should be cached.
%%%     Cache-Keys:      A list of the keys that should be cached for all 
%%%                      assignments, in addition to `/Results'.
-module(dev_process).
%%% Public API
-export([info/1, compute/3, schedule/3, slot/3, now/3, push/3, snapshot/3]).
-export([ensure_process_key/2]).
%%% Public utilities
-export([as_process/2, process_id/3]).
%%% Test helpers
-export([test_aos_process/0, test_aos_process/1, dev_test_process/0, test_wasm_process/1]).
-export([schedule_aos_call/2, schedule_aos_call/3, init/0]).
%%% Tests
-export([do_test_restore/0]).
-include_lib("eunit/include/eunit.hrl").
-include_lib("include/hb.hrl").

%% The frequency at which the process state should be cached. Can be overridden
%% with the `cache_frequency' option.
-define(DEFAULT_CACHE_FREQ, 1).

%% @doc When the info key is called, we should return the process exports.
info(_Msg1) ->
    #{
        worker => fun dev_process_worker:server/3,
        grouper => fun dev_process_worker:group/3,
        await => fun dev_process_worker:await/5,
        exclude => [
            <<"test">>,
            <<"init">>,
            <<"ping_ping_script">>,
            <<"schedule_aos_call">>,
            <<"test_aos_process">>,
            <<"dev_test_process">>,
            <<"test_wasm_process">>
        ]
    }.

%% @doc Returns the default device for a given piece of functionality. Expects
%% the `process/variant' key to be set in the message. The `execution-device'
%% _must_ be set in all processes aside those marked with `ao.TN.1' variant.
%% This is in order to ensure that post-mainnet processes do not default to
%% using infrastructure that should not be present on nodes in the future.
default_device(Msg1, Key, Opts) ->
    NormKey = hb_converge:normalize_key(Key),
    case {NormKey, hb_converge:get(<<"process/variant">>, {as, dev_message, Msg1}, Opts)} of
        {<<"execution">>, <<"ao.TN.1">>} -> <<"genesis-wasm@1.0">>;
        _ -> default_device_index(NormKey)
    end.
default_device_index(<<"scheduler">>) -> <<"scheduler@1.0">>;
default_device_index(<<"execution">>) -> <<"genesis-wasm@1.0">>;
default_device_index(<<"push">>) -> <<"push@1.0">>.

%% @doc Wraps functions in the Scheduler device.
schedule(Msg1, Msg2, Opts) ->
    run_as(<<"scheduler">>, Msg1, Msg2, Opts).

slot(Msg1, Msg2, Opts) ->
    ?event({slot_called, {msg1, Msg1}, {msg2, Msg2}, {opts, Opts}}),
    run_as(<<"scheduler">>, Msg1, Msg2, Opts).

next(Msg1, _Msg2, Opts) ->
    run_as(<<"scheduler">>, Msg1, next, Opts).

snapshot(RawMsg1, _Msg2, Opts) ->
    Msg1 = ensure_process_key(RawMsg1, Opts),
    {ok, SnapshotMsg} = run_as(
        <<"Execution">>,
        Msg1,
        #{ <<"path">> => <<"snapshot">>, <<"mode">> => <<"Map">> },
        Opts#{ cache_control => [] }
    ),
    ProcID = hb_message:id(Msg1, all),
    Slot = hb_converge:get(<<"current-slot">>, Msg1, Opts),
    {ok,
        hb_private:set(
            hb_converge:set(
                SnapshotMsg,
                #{ <<"cache-control">> => [<<"store">>] },
                Opts
            ),
            #{ <<"priv/additional-hashpaths">> =>
                    [
                        hb_path:to_binary([ProcID, <<"snapshot">>, Slot])
                    ]
            },
            Opts
        )
    }.

%% @doc Returns the process ID of the current process.
process_id(Msg1, Msg2, Opts) ->
    case hb_converge:get(<<"process">>, Msg1, Opts) of
        not_found ->
            process_id(ensure_process_key(Msg1, Opts), Msg2, Opts);
        Process ->
            hb_message:id(Process, all)
    end.

%% @doc Before computation begins, a boot phase is required. This phase
%% allows devices on the execution stack to initialize themselves. We set the
%% `Initialized' key to `True' to indicate that the process has been
%% initialized.
init(Msg1, _Msg2, Opts) ->
    ?event({init_called, {msg1, Msg1}, {opts, Opts}}),
    {ok, Initialized} =
        run_as(<<"execution">>, Msg1, #{ <<"path">> => init }, Opts),
    {
        ok,
        hb_converge:set(
            Initialized,
            #{
                <<"initialized">> => <<"true">>,
                <<"current-slot">> => -1
            },
            Opts
        )
    }.

%% @doc Compute the result of an assignment applied to the process state, if it 
%% is the next message.
compute(Msg1, Msg2, Opts) ->
    % If we do not have a live state, restore or initialize one.
    ProcBase = ensure_process_key(Msg1, Opts),
    ProcID = process_id(ProcBase, #{}, Opts),
    Slot = hb_util:int(hb_converge:get(<<"slot">>, {as, <<"message@1.0">>, Msg2}, Opts)),
    case dev_process_cache:read(ProcID, Slot, Opts) of
        {ok, Result} ->
            % The result is already cached, so we can return it.
            ?event(
                {compute_result_cached,
                    {proc_id, ProcID},
                    {slot, Slot},
                    {result, Result}
                }
            ),
            {ok, Result};
        not_found ->
            {ok, Loaded} = ensure_loaded(ProcBase, Msg2, Opts),
            ?event(compute, {computing, {process_id, ProcID}, {slot, Slot}}, Opts),
            do_compute(
                ProcID,
                Loaded,
                Msg2,
                Slot,
                Opts
            )
    end.

%% @doc Continually get and apply the next assignment from the scheduler until
%% we reach the target slot that the user has requested.
do_compute(ProcID, Msg1, Msg2, TargetSlot, Opts) ->
    CurrentSlot = hb_converge:get(<<"current-slot">>, Msg1, Opts),
    ?event({starting_compute, {current, CurrentSlot}, {target, TargetSlot}}),
    case CurrentSlot of
        CurrentSlot when CurrentSlot > TargetSlot ->
            throw(
                {error,
                    {already_calculated_slot,
                        {target, TargetSlot},
                        {current, CurrentSlot}
                    }
                }
            );
        CurrentSlot when CurrentSlot == TargetSlot ->
            % We reached the target height so we return.
            ?event(compute, {reached_target_slot_returning_state, TargetSlot}),
            {ok, as_process(Msg1, Opts)};
        CurrentSlot ->
            NextSlot = CurrentSlot + 1,
            % Get the next input from the scheduler device.
            {ok, #{ <<"body">> := ToProcess, <<"state">> := State }} =
                next(Msg1, Msg2, Opts),
            % Ensure that the next slot is the slot that we are expecting, just
            % in case there is a scheduler device error.
            NextSlot = hb_util:int(hb_converge:get(<<"slot">>, ToProcess, Opts)),
            ?event(compute, {executing, {slot, NextSlot}, {req, ToProcess}}, Opts),
            {ok, Msg3} =
                run_as(
                    <<"execution">>,
                    State,
                    ToProcess,
                    Opts
                ),
            % We have now transformed slot n -> n + 1. Increment the current slot.
            Msg3SlotAfter = hb_converge:set(Msg3, #{ <<"current-slot">> => NextSlot }, Opts),
            % Notify any waiters that the result for a slot is now available.
            dev_process_worker:notify_compute(
                ProcID,
                NextSlot,
                {ok, Msg3SlotAfter},
                Opts
            ),
            ?event(compute, {writing_cache, {proc_id, ProcID}, {slot, NextSlot}}, Opts),
            store_result(ProcID, NextSlot, Msg3, Msg2, Opts),
            ?event(compute, {wrote_cache, {proc_id, ProcID}, {slot, NextSlot}}, Opts),
            do_compute(
                ProcID,
                hb_converge:set(
                    Msg3,
                    #{ <<"current-slot">> => NextSlot },
                    Opts
                ),
                Msg2,
                TargetSlot,
                Opts
            )
    end.

%% @doc Store the resulting state in the cache, potentially with the snapshot
%% key.
store_result(ProcID, Slot, Msg3, Msg2, Opts) ->
    % Cache the `Memory' key every `Cache-Frequency' slots.
    Freq = hb_opts:get(process_cache_frequency, ?DEFAULT_CACHE_FREQ, Opts),
    Msg3MaybeWithSnapshot =
        case Slot rem Freq of
            0 ->
                case snapshot(Msg3, Msg2, Opts) of
                    {ok, Snapshot} ->
                        ?event(snapshot,
                            {got_snapshot, 
                                {storing_as_slot, Slot},
                                {snapshot, Snapshot}
                            }
                        ),
                        Msg3#{ <<"snapshot">> => Snapshot };
                    not_found ->
                        ?event(no_result_for_snapshot),
                        Msg3
                end;
            _ -> 
                Msg3
        end,
    dev_process_cache:write(ProcID, Slot, Msg3MaybeWithSnapshot, Opts).

%% @doc Returns the `/Results' key of the latest computed message.
now(RawMsg1, _Msg2, Opts) ->
    Msg1 = ensure_process_key(RawMsg1, Opts),
    {ok, CurrentSlot} = hb_converge:resolve(Msg1, #{ <<"path">> => <<"slot/current-slot">> }, Opts),
    ProcessID = process_id(Msg1, #{}, Opts),
    ?event({now_called, {process, ProcessID}, {slot, CurrentSlot}}),
    hb_converge:resolve(
        Msg1,
        #{ <<"path">> => <<"compute">>, <<"slot">> => CurrentSlot },
        Opts
    ).

%% @doc Recursively push messages to the scheduler until we find a message
%% that does not lead to any further messages being scheduled.
push(Msg1, Msg2, Opts) ->
    ProcBase = ensure_process_key(Msg1, Opts),
    run_as(<<"push">>, ProcBase, Msg2, Opts).

%% @doc Ensure that the process message we have in memory is live and
%% up-to-date.
ensure_loaded(Msg1, Msg2, Opts) ->
    % Get the nonce we are currently on and the inbound nonce.
    TargetSlot = hb_converge:get(<<"slot">>, Msg2, undefined, Opts),
    ProcID = process_id(Msg1, Msg2, Opts),
    ?event({ensure_loaded, {msg1, Msg1}, {msg2, Msg2}, {opts, Opts}}),
    case hb_converge:get(<<"initialized">>, Msg1, Opts) of
        <<"true">> ->
            ?event(already_initialized),
            {ok, Msg1};
        _ ->
            ?event(not_initialized),
            % Try to load the latest complete state from disk.
            LoadRes =
                dev_process_cache:latest(
                    ProcID,
                    [<<"snapshot">>],
                    TargetSlot,
                    Opts
                ),
            ?event(compute,
                {snapshot_load_res,
                    {proc_id, ProcID},
                    {res, LoadRes},
                    {target, TargetSlot}
                }
            ),
            case LoadRes of
                {ok, LoadedSlot, SnapshotMsg} ->
                    % Restore the devices in the executor stack with the
                    % loaded state. This allows the devices to load any
                    % necessary 'shadow' state (state not represented in
                    % the public component of a message) into memory.
                    % Do not update the hashpath while we do this.
                    ?event(compute, {loaded_state_checkpoint, ProcID, LoadedSlot}),
                    {ok, Normalized} = run_as(<<"execution">>, SnapshotMsg, normalize, Opts),
                    NormalizedWithoutSnapshot = maps:remove(<<"snapshot">>, Normalized),
                    {ok, NormalizedWithoutSnapshot};
                not_found ->
                    % If we do not have a checkpoint, initialize the
                    % process from scratch.
                    ?event(compute,
                        {no_checkpoint_found,
                            {process, ProcID},
                            {slot, TargetSlot}
                        }
                    ),
                    init(Msg1, Msg2, Opts)
            end
    end.

%% @doc Run a message against Msg1, with the device being swapped out for
%% the device found at `Key'. After execution, the device is swapped back
%% to the original device if the device is the same as we left it.
run_as(Key, Msg1, Msg2, Opts) ->
    BaseDevice = hb_converge:get(<<"device">>, {as, dev_message, Msg1}, Opts),
    ?event({running_as, {key, {explicit, Key}}, {req, Msg2}}),
    {ok, PreparedMsg} =
        dev_message:set(
            ensure_process_key(Msg1, Opts),
            #{
                <<"device">> =>
                    DeviceSet = hb_converge:get(
                        << Key/binary, "-device">>,
                        {as, dev_message, Msg1},
                        default_device(Msg1, Key, Opts),
                        Opts
                    ),
                <<"input-prefix">> =>
                    case hb_converge:get(<<"input-prefix">>, Msg1, Opts) of
                        not_found -> <<"process">>;
                        Prefix -> Prefix
                    end,
                <<"output-prefixes">> =>
                    hb_converge:get(
                        <<Key/binary, "-output-prefixes">>,
                        {as, dev_message, Msg1},
                        undefined, % Undefined in set will be ignored.
                        Opts
                    )
            },
            Opts
        ),
    {Status, BaseResult} =
        hb_converge:resolve(
            PreparedMsg,
            Msg2,
            Opts
        ),
    case {Status, BaseResult} of
        {ok, #{ <<"device">> := DeviceSet }} ->
            {ok, hb_converge:set(BaseResult, #{ <<"device">> => BaseDevice })};
        _ ->
            ?event({returning_base_result, BaseResult}),
            {Status, BaseResult}
    end.

%% @doc Change the message to for that has the device set as this module.
%% In situations where the key that is `run_as' returns a message with a 
%% transformed device, this is useful.
as_process(Msg1, Opts) ->
    {ok, Proc} = dev_message:set(Msg1, #{ <<"device">> => <<"process@1.0">> }, Opts),
    Proc.

%% @doc Helper function to store a copy of the `process' key in the message.
ensure_process_key(Msg1, Opts) ->
    case hb_converge:get(<<"process">>, Msg1, Opts) of
        not_found ->
            hb_converge:set(
                Msg1,
                #{ <<"process">> => Msg1 },
                Opts#{ hashpath => ignore }
            );
        _ -> Msg1
    end.

%%% Tests

init() ->
    application:ensure_all_started(hb),
    ok.

%% @doc Generate a process message with a random number, and no 
%% executor.
test_base_process() ->
    test_base_process(#{}).
test_base_process(Opts) ->
    Wallet = hb_opts:get(priv_wallet, hb:wallet(), Opts),
    Address = hb_util:human_id(ar_wallet:to_address(Wallet)),
    hb_message:attest(#{
        <<"device">> => <<"Process@1.0">>,
        <<"scheduler-device">> => <<"Scheduler@1.0">>,
        <<"scheduler-location">> => Address,
        <<"type">> => <<"Process">>,
        <<"test-random-seed">> => rand:uniform(1337)
    }, Wallet).

test_wasm_process(WASMImage) ->
    test_wasm_process(WASMImage, #{}).
test_wasm_process(WASMImage, Opts) ->
    Wallet = hb_opts:get(priv_wallet, hb:wallet(), Opts),
    #{ <<"image">> := WASMImageID } = dev_wasm:cache_wasm_image(WASMImage, Opts),
    hb_message:attest(
        maps:merge(test_base_process(Opts), #{
            <<"execution-device">> => <<"Stack@1.0">>,
            <<"device-stack">> => [<<"WASM-64@1.0">>],
            <<"image">> => WASMImageID
        }),
        Wallet
    ).

%% @doc Generate a process message with a random number, and the 
%% `dev_wasm' device for execution.
test_aos_process() ->
    test_aos_process(#{}).
test_aos_process(Opts) ->
    test_aos_process(Opts, [
        <<"WASI@1.0">>,
        <<"JSON-Iface@1.0">>,
        <<"WASM-64@1.0">>,
        <<"Multipass@1.0">>
    ]).
test_aos_process(Opts, Stack) ->
    Wallet = hb_opts:get(priv_wallet, hb:wallet(), Opts),
    Address = hb_util:human_id(ar_wallet:to_address(Wallet)),
    WASMProc = test_wasm_process(<<"test/aos-2-pure-xs.wasm">>, Opts),
    hb_message:attest(maps:merge(WASMProc, #{
        <<"device-stack">> => Stack,
        <<"execution-device">> => <<"stack@1.0">>,
        <<"scheduler-device">> => <<"scheduler@1.0">>,
        <<"output-prefix">> => <<"wasm">>,
        <<"passes">> => 2,
        <<"stack-keys">> =>
            [
                <<"init">>,
                <<"compute">>,
                <<"snapshot">>,
                <<"normalize">>
            ],
        <<"scheduler">> => Address,
        <<"authority">> => Address
    }), Wallet).

%% @doc Generate a device that has a stack of two `dev_test's for 
%% execution. This should generate a message state has doubled 
%% `Already-Seen' elements for each assigned slot.
dev_test_process() ->
    Wallet = hb:wallet(),
    hb_message:attest(
        maps:merge(test_base_process(), #{
            <<"execution-device">> => <<"Stack@1.0">>,
            <<"device-stack">> => [<<"Test-Device@1.0">>, <<"Test-Device@1.0">>]
        }),
        Wallet
    ).

schedule_test_message(Msg1, Text) ->
    schedule_test_message(Msg1, Text, #{}).
schedule_test_message(Msg1, Text, MsgBase) ->
    Wallet = hb:wallet(),
    Msg2 = hb_message:attest(#{
        <<"path">> => <<"schedule">>,
        <<"method">> => <<"POST">>,
        <<"body">> =>
            hb_message:attest(
                MsgBase#{
                    <<"type">> => <<"Message">>,
                    <<"test-label">> => Text
                },
                Wallet
            )
    }, Wallet),
    {ok, _} = hb_converge:resolve(Msg1, Msg2, #{}).

schedule_aos_call(Msg1, Code) ->
    schedule_aos_call(Msg1, Code, #{}).
schedule_aos_call(Msg1, Code, Opts) ->
    Wallet = hb_opts:get(priv_wallet, hb:wallet(), Opts),
    ProcID = hb_message:id(Msg1, all),
    Msg2 =
        hb_message:attest(
            #{
                <<"action">> => <<"Eval">>,
                <<"data">> => Code,
                <<"target">> => ProcID
            },
            Wallet
        ),
    schedule_test_message(Msg1, <<"TEST MSG">>, Msg2).

schedule_wasm_call(Msg1, FuncName, Params) ->
    schedule_wasm_call(Msg1, FuncName, Params, #{}).
schedule_wasm_call(Msg1, FuncName, Params, Opts) ->
    Wallet = hb:wallet(),
    Msg2 = hb_message:attest(#{
        <<"path">> => <<"schedule">>,
        <<"method">> => <<"POST">>,
        <<"body">> =>
            hb_message:attest(
                #{
                    <<"type">> => <<"Message">>,
                    <<"wasm-function">> => FuncName,
                    <<"wasm-params">> => Params
                },
                Wallet
            )
    }, Wallet),
    ?assertMatch({ok, _}, hb_converge:resolve(Msg1, Msg2, Opts)).

schedule_on_process_test() ->
    init(),
    Msg1 = test_aos_process(),
    schedule_test_message(Msg1, <<"TEST TEXT 1">>),
    schedule_test_message(Msg1, <<"TEST TEXT 2">>),
    ?event(messages_scheduled),
    {ok, SchedulerRes} =
        hb_converge:resolve(Msg1, #{
            <<"method">> => <<"GET">>,
            <<"path">> => <<"schedule">>
        }, #{}),
    ?assertMatch(
        <<"TEST TEXT 1">>,
        hb_converge:get(<<"assignments/0/body/Test-Label">>, SchedulerRes)
    ),
    ?assertMatch(
        <<"TEST TEXT 2">>,
        hb_converge:get(<<"assignments/1/body/Test-Label">>, SchedulerRes)
    ).

get_scheduler_slot_test() ->
    init(),
    Msg1 = test_base_process(),
    schedule_test_message(Msg1, <<"TEST TEXT 1">>),
    schedule_test_message(Msg1, <<"TEST TEXT 2">>),
    Msg2 = #{
        <<"path">> => <<"Slot">>,
        <<"method">> => <<"GET">>
    },
    ?assertMatch(
        {ok, #{ <<"current-slot">> := CurrentSlot }} when CurrentSlot > 0,
        hb_converge:resolve(Msg1, Msg2, #{})
    ).

recursive_path_resolution_test() ->
    init(),
    Msg1 = test_base_process(),
    schedule_test_message(Msg1, <<"TEST TEXT 1">>),
    CurrentSlot =
        hb_converge:resolve(
            Msg1,
            #{ <<"path">> => <<"slot/current-slot">> },
            #{ <<"hashpath">> => ignore }
        ),
    ?event({resolved_current_slot, CurrentSlot}),
    ?assertMatch(
        CurrentSlot when CurrentSlot > 0,
        CurrentSlot
    ),
    ok.

test_device_compute_test() ->
    init(),
    Msg1 = dev_test_process(),
    schedule_test_message(Msg1, <<"TEST TEXT 1">>),
    schedule_test_message(Msg1, <<"TEST TEXT 2">>),
    ?assertMatch(
        {ok, <<"TEST TEXT 2">>},
        hb_converge:resolve(
            Msg1,
            <<"schedule/assignments/1/body/test-label">>,
            #{ <<"hashpath">> => ignore }
        )
    ),
    Msg2 = #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
    {ok, Msg3} = hb_converge:resolve(Msg1, Msg2, #{}),
    ?event({computed_message, {msg3, Msg3}}),
    ?assertEqual(1, hb_converge:get(<<"results/assignment-slot">>, Msg3, #{})),
    ?assertEqual([1,1,0,0], hb_converge:get(<<"already-seen">>, Msg3, #{})).

wasm_compute_test() ->
    init(),
    Msg1 = test_wasm_process(<<"test/test-64.wasm">>),
    schedule_wasm_call(Msg1, <<"fac">>, [5.0]),
    schedule_wasm_call(Msg1, <<"fac">>, [6.0]),
    {ok, Msg3} = 
        hb_converge:resolve(
            Msg1,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 0 },
            #{ <<"hashpath">> => ignore }
        ),
    ?event({computed_message, {msg3, Msg3}}),
    ?assertEqual([120.0], hb_converge:get(<<"results/output">>, Msg3, #{})),
    {ok, Msg4} = 
       hb_converge:resolve(
            Msg1,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
            #{ <<"hashpath">> => ignore }
        ),
    ?event({computed_message, {msg4, Msg4}}),
    ?assertEqual([720.0], hb_converge:get(<<"results/output">>, Msg4, #{})).

wasm_compute_from_id_test() ->
    init(),
    Opts = #{ cache_control => <<"always">> },
    Msg1 = test_wasm_process(<<"test/test-64.wasm">>),
    schedule_wasm_call(Msg1, <<"fac">>, [5.0], Opts),
    Msg1ID = hb_message:id(Msg1),
    Msg2 = #{ <<"path">> => <<"compute">>, <<"slot">> => 0 },
    {ok, Msg3} = hb_converge:resolve(Msg1ID, Msg2, Opts),
    ?event(process_compute, {computed_message, {msg3, Msg3}}),
    ?assertEqual([120.0], hb_converge:get(<<"results/output">>, Msg3, Opts)).

http_wasm_process_by_id_test() ->
    rand:seed(default),
    SchedWallet = ar_wallet:new(),
    Node = hb_http_server:start_node(Opts = #{
        port => 10000 + rand:uniform(10000),
        priv_wallet => SchedWallet,
        cache_control => <<"always">>,
        store => {hb_store_fs, #{ prefix => "mainnet-cache" }}
    }),
    Wallet = ar_wallet:new(),
    Proc = test_wasm_process(<<"test/test-64.wasm">>, Opts),
    hb_cache:write(Proc, Opts),
    ProcID = hb_util:human_id(hb_message:id(Proc)),
    InitRes =
        hb_http:post(
            Node,
            << "/schedule" >>,
            Proc,
            #{}
        ),
    ?event({schedule_proc_res, InitRes}),
    ExecMsg =
        hb_message:attest(#{
            <<"target">> => ProcID,
            <<"type">> => <<"Message">>,
            <<"wasm-function">> => <<"fac">>,
            <<"wasm-params">> => [5.0]
        },
        Wallet
    ),
    {ok, Msg3} =
        hb_http:post(
            Node,
            << ProcID/binary, "/schedule">>,
            ExecMsg,
            #{}
        ),
    ?event({schedule_msg_res, {msg3, Msg3}}),
    {ok, Msg4} =
        hb_http:get(
            Node,
            #{
                <<"path">> => << ProcID/binary, "/compute">>,
                <<"slot">> => 1
            },
            #{}
        ),
    ?event({compute_msg_res, {msg4, Msg4}}),
    ?assertEqual([120.0], hb_converge:get(<<"results/output">>, Msg4, #{})).

aos_compute_test_() ->
    {timeout, 30, fun() ->
        init(),
        Msg1 = test_aos_process(),
        schedule_aos_call(Msg1, <<"return 1+1">>),
        schedule_aos_call(Msg1, <<"return 2+2">>),
        Msg2 = #{ <<"path">> => <<"compute">>, <<"slot">> => 0 },
        {ok, Msg3} = hb_converge:resolve(Msg1, Msg2, #{}),
        {ok, Res} = hb_converge:resolve(Msg3, <<"results">>, #{}),
        ?event({computed_message, {msg3, Res}}),
        {ok, Data} = hb_converge:resolve(Res, <<"data">>, #{}),
        ?event({computed_data, Data}),
        ?assertEqual(<<"2">>, Data),
        Msg4 = #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
        {ok, Msg5} = hb_converge:resolve(Msg1, Msg4, #{}),
        ?assertEqual(<<"4">>, hb_converge:get(<<"results/data">>, Msg5, #{})),
        {ok, Msg5}
    end}.

aos_browsable_state_test_() ->
    {timeout, 30, fun() ->
        init(),
        Msg1 = test_aos_process(),
        schedule_aos_call(Msg1,
            <<"table.insert(ao.outbox.Messages, { Target = ao.id, ",
                "Action = \"State\", ",
                "Data = { Deep = 4, Bool = true } })">>
        ),
        Msg2 = #{ <<"path">> => <<"compute">>, <<"slot">> => 0 },
        {ok, Msg3} =
            hb_converge:resolve_many(
                [Msg1, Msg2, <<"results">>, <<"outbox">>, 1, <<"data">>, <<"Deep">>],
                #{ cache_control => <<"always">> }
            ),
        ID = hb_message:id(Msg1),
        ?event({computed_message, {id, {explicit, ID}}}),
        ?assertEqual(4, Msg3)
    end}.

aos_state_access_via_http_test_() ->
    {timeout, 60, fun() ->
        rand:seed(default),
        Wallet = ar_wallet:new(),
        Node = hb_http_server:start_node(Opts = #{
            port => 10000 + rand:uniform(10000),
            priv_wallet => Wallet,
            cache_control => <<"always">>,
            store => {hb_store_fs, #{ prefix => "mainnet-cache" }},
            force_signed_requests => true
        }),
        Proc = test_aos_process(Opts),
        ProcID = hb_util:human_id(hb_message:id(Proc, all)),
        {ok, _InitRes} =
            hb_http:post(
                Node,
                << "/schedule" >>,
                Proc,
                Opts
            ),
        Msg2 = hb_message:attest(#{
            <<"data-prefix">> => <<"ao">>,
            <<"variant">> => <<"ao.N.1">>,
            <<"type">> => <<"Message">>,
            <<"action">> => <<"Eval">>,
            <<"data">> =>
                <<"table.insert(ao.outbox.Messages, { Target = ao.id,",
                    " Action = \"State\", Data = { ",
                        "[\"content-type\"] = \"text/html\", ",
                        "[\"body\"] = \"<h1>Hello, world!</h1>\"",
                    "}})">>,
            <<"target">> => ProcID
        }, Wallet),
        {ok, Msg3} =
            hb_http:post(
                Node,
                << ProcID/binary, "/schedule">>,
                Msg2,
                Opts
            ),
        ?event({schedule_msg_res, {msg3, Msg3}}),
        {ok, Msg4} =
            hb_http:get(
                Node,
                #{
                    <<"path">> =>
                        <<
                            ProcID/binary,
                            "/compute/results/outbox/1/data"
                        >>,
                    <<"slot">> => 1
                },
                Opts
            ),
        ?event({compute_msg_res, {msg4, Msg4}}),
        ?event(
            {try_yourself,
                {explicit,
                    << Node/binary, "/", ProcID/binary, "/compute&slot=1/results/outbox/1/data">>}
            }
        ),
        ?assertMatch(#{ <<"body">> := <<"<h1>Hello, world!</h1>">> }, Msg4),
        ok
    end}.

aos_state_patch_test_() ->
    {timeout, 30, fun() ->
        Wallet = hb:wallet(),
        init(),
        Msg1 = test_aos_process(#{}, [
            <<"WASI@1.0">>,
            <<"JSON-Iface@1.0">>,
            <<"WASM-64@1.0">>,
            <<"patch@1.0">>,
            <<"Multipass@1.0">>
        ]),
        ProcID = hb_message:id(Msg1),
        Msg2 = (hb_message:attest(#{
            <<"data-prefix">> => <<"ao">>,
            <<"variant">> => <<"ao.N.1">>,
            <<"target">> => ProcID,
            <<"type">> => <<"Message">>,
            <<"action">> => <<"Eval">>,
            <<"data">> => <<"table.insert(ao.outbox.Messages, { method = \"PATCH\", x = \"banana\" })">>
        }, Wallet))#{ <<"path">> => <<"schedule">>, <<"method">> => <<"POST">> },
        {ok, _} = hb_converge:resolve(Msg1, Msg2, #{}),
        Msg3 = #{ <<"path">> => <<"compute">>, <<"slot">> => 0 },
        {ok, Msg4} = hb_converge:resolve(Msg1, Msg3, #{}),
        ?event({computed_message, {msg3, Msg4}}),
        {ok, Data} = hb_converge:resolve(Msg4, <<"x">>, #{}),
        ?event({computed_data, Data}),
        ?assertEqual(<<"banana">>, Data)
    end}.

%% @doc Manually test state restoration without using the cache.
restore_test_() -> {timeout, 30, fun do_test_restore/0}.

do_test_restore() ->
    % Init the process and schedule 3 messages:
    % 1. Set variables in Lua.
    % 2. Return the variable.
    % Execute the first computation, then the second as a disconnected process.
    Opts = #{ process_cache_frequency => 1 },
    init(),
    Store = hb_opts:get(store, no_viable_store, Opts),
    ResetRes = hb_store:reset(Store),
    ?event({reset_store, {result, ResetRes}, {store, Store}}),
    Msg1 = test_aos_process(),
    schedule_aos_call(Msg1, <<"X = 42">>),
    schedule_aos_call(Msg1, <<"X = 1337">>),
    schedule_aos_call(Msg1, <<"return X">>),
    % Compute the first message.
    {ok, _} =
        hb_converge:resolve(
            Msg1,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
            Opts
        ),
    {ok, ResultB} =
        hb_converge:resolve(
            Msg1,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 2 },
            Opts
        ),
    ?event({result_b, ResultB}),
    ?assertEqual(<<"1337">>, hb_converge:get(<<"results/data">>, ResultB, #{})).

now_results_test_() ->
    {timeout, 30, fun() ->
        init(),
        Msg1 = test_aos_process(),
        schedule_aos_call(Msg1, <<"return 1+1">>),
        schedule_aos_call(Msg1, <<"return 2+2">>),
        ?assertEqual({ok, <<"4">>}, hb_converge:resolve(Msg1, <<"now/results/data">>, #{}))
    end}.

prior_results_accessible_test() ->
    init(),
    Msg1 = test_aos_process(),
    schedule_aos_call(Msg1, <<"return 1+1">>),
    schedule_aos_call(Msg1, <<"return 2+2">>),
    ?assertEqual({ok, <<"4">>}, hb_converge:resolve(Msg1, <<"now/results/data">>, #{})),
    ?assertMatch({ok, #{ <<"results">> := #{ <<"data">> := <<"4">> } }},
        hb_converge:resolve(
            Msg1,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
            #{}
        )
    ).

persistent_process_test() ->
    {timeout, 30, fun() ->
        init(),
        Msg1 = test_aos_process(),
        schedule_aos_call(Msg1, <<"X=1">>),
        schedule_aos_call(Msg1, <<"return 2">>),
        schedule_aos_call(Msg1, <<"return X">>),
        T0 = hb:now(),
        FirstSlotMsg2 = #{
            <<"path">> => <<"compute">>,
            <<"slot">> => 0
        },
        ?assertMatch(
            {ok, _},
            hb_converge:resolve(Msg1, FirstSlotMsg2, #{ spawn_worker => true })
        ),
        T1 = hb:now(),
        ThirdSlotMsg2 = #{
            <<"path">> => <<"compute">>,
            <<"slot">> => 2
        },
        Res = hb_converge:resolve(Msg1, ThirdSlotMsg2, #{}),
        ?event({computed_message, {msg3, Res}}),
        ?assertMatch(
            {ok, _},
            Res
        ),
        T2 = hb:now(),
        ?event(benchmark, {runtimes, {first_run, T1 - T0}, {second_run, T2 - T1}}),
        % The second resolve should be much faster than the first resolve, as the
        % process is already running.
        ?assert(T2 - T1 < ((T1 - T0)/2))
    end}.

simple_wasm_persistent_worker_benchmark_test() ->
    init(),
    BenchTime = 1,
    Msg1 = test_wasm_process(<<"test/test-64.wasm">>),
    schedule_wasm_call(Msg1, <<"fac">>, [5.0]),
    schedule_wasm_call(Msg1, <<"fac">>, [6.0]),
    {ok, Initialized} = 
        hb_converge:resolve(
            Msg1,
            #{ <<"path">> => <<"compute">>, <<"slot">> => 1 },
            #{ spawn_worker => true, process_workers => true }
        ),
    Iterations = hb:benchmark(
        fun(Iteration) ->
            schedule_wasm_call(
                Initialized,
                <<"fac">>,
                [5.0]
            ),
            ?assertMatch(
                {ok, _},
                hb_converge:resolve(
                    Initialized,
                    #{ <<"path">> => <<"compute">>, <<"slot">> => Iteration + 1 },
                    #{}
                )
            )
        end,
        BenchTime
    ),
    ?event(benchmark, {scheduled, Iterations}),
    hb_util:eunit_print(
        "Scheduled and evaluated ~p simple wasm process messages in ~p s (~.2f msg/s)",
        [Iterations, BenchTime, Iterations / BenchTime]
    ),
    ?assert(Iterations > 2),
    ok.

aos_persistent_worker_benchmark_test_() ->
    {timeout, 30, fun() ->
        BenchTime = 4,
        init(),
        Msg1 = test_aos_process(),
        schedule_aos_call(Msg1, <<"X=1337">>),
        FirstSlotMsg2 = #{
            <<"path">> => <<"compute">>,
            <<"slot">> => 0
        },
        ?assertMatch(
            {ok, _},
            hb_converge:resolve(Msg1, FirstSlotMsg2, #{ spawn_worker => true })
        ),
        Iterations = hb:benchmark(
            fun(Iteration) ->
                schedule_aos_call(
                    Msg1,
                    <<"return X + ", (integer_to_binary(Iteration))/binary>>
                ),
                ?assertMatch(
                    {ok, _},
                    hb_converge:resolve(
                        Msg1,
                        #{ <<"path">> => <<"compute">>, <<"slot">> => Iteration },
                        #{}
                    )
                )
            end,
            BenchTime
        ),
        ?event(benchmark, {scheduled, Iterations}),
        hb_util:eunit_print(
            "Scheduled and evaluated ~p AOS process messages in ~p s (~.2f msg/s)",
            [Iterations, BenchTime, Iterations / BenchTime]
        ),
        ?assert(Iterations >= 2),
        ok
    end}.